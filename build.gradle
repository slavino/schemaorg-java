apply plugin: 'java'
apply plugin: 'java-library-distribution'
apply plugin: 'maven'

repositories {
    mavenCentral()
}

project.version '1.0.0'

dependencies {
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'com.google.guava:guava:24.1-jre'
    compile 'com.google.code.gson:gson:2.6.2'
    testCompile 'junit:junit:4.+'
    testCompile 'com.google.truth:truth:0.28'
}

jar {
    baseName 'schema-org-client'
}

task fatJar(type: Jar) {
    baseName = 'schema-org-client-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task sourcesJar(type: Jar, dependsOn: classes) {
    baseName 'schema-org-client'
    classifier 'sources'
    from sourceSets.main.allSource
}

task createPom << {
    pom {
        project {
            groupId 'com.google.schemaorg'
            artifactId 'schema-org-client'
            version '1.0.1-SNAPSHOT'

            inceptionYear '2016'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }.writeTo("pom.xml")
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'export'
       }
    }
}

test {
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true
}

distributions {
    main {
        baseName = 'schema-org-client'
        contents {
            from { sourcesJar }
            from { 'README.md' }
            from { 'LICENSE' }
        }
    }
}

